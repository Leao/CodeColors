apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            url "$rootDir/" + RESPOSITORY_LOCAL_LIBRARY
        }
    }

    publications {
        CodeColors(MavenPublication) {
            groupId GROUP_ID
            artifactId ARTIFACT_LIBRARY
            version VERSION

            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/codecolors-release.aar")

            pom.withXml {
                def projectNode = asNode();

                // Iterate over the compile dependencies to add a node for every valid one.
                def dependenciesNode
                configurations.compile.allDependencies.each {
                    if (it.group != null && it.name != null && it.version != null) {
                        if (dependenciesNode == null) {
                            // Only create dependencies node if there are valid dependencies.
                            dependenciesNode = projectNode.appendNode('dependencies')
                        }

                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }

                projectNode.appendNode('organization')
                                .appendNode('name', POM_ORGANIZATION_NAME).parent()
                                .appendNode('url', POM_ORGANIZATION_URL)

                projectNode.appendNode('scm')
                                .appendNode('url', POM_SCM_URL).parent()
                                .appendNode('connection', POM_SCM_CONNECTION).parent()
                                .appendNode('developerConnection', POM_SCM_DEV_CONNECTION)

                projectNode.appendNode('issueManagement')
                                .appendNode('system', POM_ISSUEMGMT_SYSTEM).parent()
                                .appendNode('url', POM_ISSUEMGMT_URL)

                projectNode.appendNode('licenses')
                                .appendNode('license')
                                    .appendNode('name', POM_LICENSE_NAME).parent()
                                    .appendNode('url', POM_LICENSE_URL).parent()
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['CodeColors']
    publish = true
    pkg {
        repo = 'maven'
        name = ARTIFACT_LIBRARY
        userOrg = BINTRAY_USER_ORG
        licenses = [BINTRAY_LICENSE]
        vcsUrl = BINTRAY_VCS_URL
        labels = ['android', 'library']
        attributes = ['gradle-plugin': GROUP_ID + ':' + ARTIFACT_PLUGIN + ":" + VERSION]
        publicDownloadNumbers = false
        version {
            name = VERSION_NAME
            desc = BINTRAY_LIBRARY_DESC
            vcsTag = VERSION
        }
    }
}
